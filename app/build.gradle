apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.kennethfechter.calculendar"
        minSdkVersion 23
        targetSdkVersion 30
        if(System.getenv("CI_PIPELINE_IID") != null) {
            versionCode = (34686956 + System.getenv("CI_PIPELINE_IID").toInteger())
            versionName = new Date().format('yyyyMMdd') + "-" + System.getenv("CI_COMMIT_SHORT_SHA")
        } else {
            versionCode = 2
            versionName = new Date().format('yyyyMMdd') + "-DevBuild"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("calculendar-upload-store.jks")
            storePassword System.getenv("KEYSTORE_MASTER")
            keyAlias "calculendar-release-key"
            keyPassword System.getenv("RELEASE_SIGNING")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        animationsDisabled = true
    }
}

project.afterEvaluate {
    if (project.hasProperty("assembleRelease")) {
        assembleRelease.dependsOn connectedAndroidTest
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines"
    implementation 'com.squareup:android-times-square:1.6.5@aar'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation "androidx.test:rules:1.3.0"
}

apply plugin: 'com.google.gms.google-services'
